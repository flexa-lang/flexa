using flx.std.math;


/**
 * https://projecteuler.net/problem=N
 */

/**
 * Problem 1: Multiples of 3 or 5
 * If we list all the natural numbers below 10 that are multiples of 3 or 5, we get 3, 5, 6 and 9. The sum of these multiples is 23.
 * Find the sum of all the multiples of 3 or 5 below 1000.
 */
fun problem1() {
    var result: int = 0;

    for (var i = 0; i < 1000; i++) {
        if (i % 3 == 0 or i % 5 == 0) {
            result += i;
        }
    }

    println("---------- Problem 1 ----------");
    print("The sum of all the multiples of 3 or 5 below 1000 is: ", result);
    println("\n-------------------------------\n");
}

/**
 * Problem 2: Even Fibonacci Numbers
 * Each new term in the Fibonacci sequence is generated by adding the previous two terms. By starting with 1 and 2, the first 10 terms will be:
 * 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
 * By considering the terms in the Fibonacci sequence whose values do not exceed four million, find the sum of the even-valued terms.
 */
fun problem2() {
    var a = 2;
    var b = 8;
    var sum = 2;
    var limit = 4000000;

    while (b <= limit) {
        sum += b;
        var next = 4 * b + a;
        a = b;
        b = next;
    }

    println("---------- Problem 2 ----------");
    print("The sum of the even-valued terms in the Fibonacci sequence whose values do not exceed four million is: ", sum);
    println("\n-------------------------------\n");
}


/**
 * Problem 3: Largest Prime Factor
 * The prime factors of 13195 are 5, 7, 13 and 29.
 * What is the largest prime factor of the number 600851475143?
 */
fun problem3() {
    var number = 600851475143;
    var factor = 2;

    while (number % factor == 0) {
        number /= factor;
    }

    factor = 3;
    var largest = 2;

    while (factor * factor <= number) {
        if (number % factor == 0) {
            number /= factor;
            largest = factor;
        } else {
            factor += 2;  // só ímpares
        }
    }

    if (number > 1) {
        largest = number;
    }

    println("---------- Problem 3 ----------");
    print("The largest prime factor of the number 600851475143 is: ", int(largest));
    println("\n-------------------------------\n");
}

fun is_palindrome(n: int): bool {
    var original = n;
    var reversed = 0;

    while (n > 0) {
        reversed = reversed * 10 + (n % 10);
        n /= 10;
    }

    return original == reversed;
}

/**
 * Problem 4: Largest Palindrome Product
 * A palindromic number reads the same both ways. The largest palindrome made from the product of two 2-digit numbers is 
 * 9009 = 91 x 99.
 * Find the largest palindrome made from the product of two 3-digit numbers.
 */
fun problem4() {
    var largest_palindrome = 0;

    for (var i = 999; i >= 100; i--) {
        for (var j = i; j >= 100; j--) {
            var product = i * j;
            
            if (product <= largest_palindrome) {
                break;
            }

            if (is_palindrome(product)) {
                largest_palindrome = product;
            }
        }
    }

    println("---------- Problem 4 ----------");
    print("The largest palindrome made from the product of two 3-digit numbers is: ", largest_palindrome);
    println("\n-------------------------------\n");

}

// MDC using Euclides algorithm
fun gcd(a: int, b: int): int {
    while (b != 0) {
        var temp = b;
        b = a % b;
        a = temp;
    }
    return a;
}

// calculate MMC for two numbers
fun lcm(a: int, b: int): int {
    return a * b / gcd(a, b);
}

/**
 * Problem 5: Smallest Multiple
 * 2520 is the smallest number that can be divided by each of the numbers from 1 to 10 without any remainder.
 * What is the smallest positive number that is evenly divisible by all of the numbers from 1 to 20?
 */
fun problem5() {
    var smal_even_div = 1;

    for (var i = 2; i <= 20; i++) {
        smal_even_div = lcm(smal_even_div, i);
    }

    println("---------- Problem 5 ----------");
    print("The smallest positive number that is evenly divisible by all of the numbers from 1 to 20 is: ", smal_even_div);
    println("\n-------------------------------\n");

}

fun main() {
    while (true) {
        println("===== Project Euler Flexa Edition =====");
        println("Select a problem to run:");
        println("0 - Run all problems");
        println("1 - Multiples of 3 or 5");
        println("2 - Even Fibonacci Numbers");
        println("3 - Largest Prime Factor");
        println("4 - Largest Palindrome Product");
        println("5 - Smallest Multiple");
        println("9 - Exit");
        print("Enter your choice: ");

        var choice: int = int(read());

        switch (choice) {
            case 1:
                problem1();
                break;
            case 2:
                problem2();
                break;
            case 3:
                problem3();
                break;
            case 4:
                problem4();
                break;
            case 5:
                problem5();
                break;
            case 0:
                println("Running all problems...\n");
                problem1();
                problem2();
                problem3();
                problem4();
                problem5();
                break;
            case 9:
                println("Exiting program...");
            default:
                println("Invalid option. Please try again.");
        }

        if (choice == 0) {
            break;
        }

        println();
    }
}

main();
