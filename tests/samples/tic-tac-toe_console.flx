using flx.std.strings;
using flx.core.os;

// --------------------------------------------------
// Game constants
// --------------------------------------------------
const BOARD_SIZE = 3;

// --------------------------------------------------
// TicTacToe game class
// --------------------------------------------------
class TicTacToe {
	var board: int[3][3] = { 0 };
	var current_player: int;
	var game_over: bool;
	var winner: int;

	var cursor_row: int;
	var cursor_col: int;

	// Constructor
	init() {
		self.reset();
	}

	// Reset the game state
	fun reset() {
		for (var r = 0; r < BOARD_SIZE; r++) {
			for (var c = 0; c < BOARD_SIZE; c++) {
				self.board[r][c] = 0;
			}
		}

		self.current_player = 1; // 1 = X, 2 = O
		self.game_over = false;
		self.winner = 0;

		self.cursor_row = 0;
		self.cursor_col = 0;
	}

	// Main game loop
	fun run() {
		println("=== Tic-Tac-Toe (Console) ===");
		println("Use arrow keys to move, ENTER to place.");

		while (not self.game_over) {
			self.render();
			self.handle_input();
		}

		self.render();
		self.print_result();
	}

	// Render the board with cursor highlight
	fun render() {
		self.clear();
		println("");

		for (var r = 0; r < BOARD_SIZE; r++) {
			var line = "";

			for (var c = 0; c < BOARD_SIZE; c++) {
				var cell = self.board[r][c];
				var symbol = " ";

				if (cell == 1) {
					symbol = "X";
				}
				else if (cell == 2) {
					symbol = "O";
				}
				// print();
				if (r == self.cursor_row and c == self.cursor_col) {
					line += "[" + symbol + "]";
				} else {
					line += " " + symbol + " ";
				}

				if (c < BOARD_SIZE - 1) {
					line += "|";
				}
			}

			println(line);

			if (r < BOARD_SIZE - 1) {
				println("---+---+---");
			}
		}

		println("");
		var p = self.current_player == 1 ? "X" : "O";
		println("Current player: " + p);
	}

	// Handle keyboard input (single key)
	fun handle_input() {
		var input = int(readch());
		println(input);

		if (input in { 65, 72 }) {
			// Up
			if (self.cursor_row > 0) {
				self.cursor_row -= 1;
			}
		}
		else if (input in { 66, 80 }) {
			// Down
			if (self.cursor_row < BOARD_SIZE - 1) {
				self.cursor_row += 1;
			}
		}
		else if (input in { 68, 75 }) {
			// Left
			if (self.cursor_col > 0) {
				self.cursor_col -= 1;
			}
		}
		else if (input in { 67, 77 }) {
			// Right
			if (self.cursor_col < BOARD_SIZE - 1) {
				self.cursor_col += 1;
			}
		}
		else if (input in { 10, 13 }) {
			// Enter: try to place mark
			self.try_place();
		}
		else {
			// Ignore other keys
		}
	}

	// Try to place the current player's mark
	fun try_place() {
		if (self.board[self.cursor_row][self.cursor_col] != 0) {
			return;
		}

		self.board[self.cursor_row][self.cursor_col] = self.current_player;

		if (self.check_winner()) {
			self.game_over = true;
			self.winner = self.current_player;
			return;
		}

		if (self.is_draw()) {
			self.game_over = true;
			self.winner = 0;
			return;
		}

		self.switch_player();
	}

	// Switch players
	fun switch_player() {
		self.current_player = self.current_player == 1 ? 2 : 1;
	}

	// Check for a winner
	fun check_winner(): bool {
		for (var i = 0; i < 3; i++) {
			if (self.board[i][0] != 0 and
				self.board[i][0] == self.board[i][1] and
				self.board[i][1] == self.board[i][2]) {
				return true;
			}

			if (self.board[0][i] != 0 and
				self.board[0][i] == self.board[1][i] and
				self.board[1][i] == self.board[2][i]) {
				return true;
			}
		}

		if (self.board[0][0] != 0 and
			self.board[0][0] == self.board[1][1] and
			self.board[1][1] == self.board[2][2]) {
			return true;
		}

		if (self.board[0][2] != 0 and
			self.board[0][2] == self.board[1][1] and
			self.board[1][1] == self.board[2][0]) {
			return true;
		}

		return false;
	}

	// Check draw
	fun is_draw(): bool {
		for (var r = 0; r < 3; r++) {
			for (var c = 0; c < 3; c++) {
				if (self.board[r][c] == 0) {
					return false;
				}
			}
		}
		return true;
	}

	// Print final result
	fun print_result() {
		println("");

		if (self.winner == 0) {
			println("Game ended in a draw.");
		}
		else {
			var p = self.winner == 1 ? "X" : "O";
			println("Player " + p + " wins!");
		}
	}

	fun clear() {
		if (flx::os.name == "windows") {
			system("cls");
		} else if (flx::os.name == "linux") {
			system("clear");
		}
	}
}

// --------------------------------------------------
// Main entry point
// --------------------------------------------------
fun main() {
	var game = TicTacToe();
	game.run();
}

main();
